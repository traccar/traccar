plugins {
    id "java"
    id "checkstyle"
    id "com.google.protobuf" version "0.8.18"
    id "org.kordamp.gradle.project-enforcer" version "0.9.0"
}

repositories {
    mavenCentral()
}

ext {
    guiceVersion = "5.0.1"
    jettyVersion = "10.0.7" // jetty 11 javax to jakarta
    jerseyVersion = "2.35" // jersey 3 javax to jakarta
    jacksonVersion = "2.12.2" // same version as jersey-media-json-jackson dependency
    protobufVersion = "3.19.3"
}

sourceCompatibility = "11"
compileJava.options.encoding = "UTF-8"
jar.destinationDirectory = file("$projectDir/target")

checkstyle {
    toolVersion = "9.2.1"
    configFile = "gradle/checkstyle.xml" as File
    checkstyleTest.enabled = false
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }
}

enforce {
    rule(enforcer.rules.EnforceBytecodeVersion) { r ->
        r.maxJdkVersion = "11"
    }
}

dependencies {
    implementation "commons-codec:commons-codec:1.15"
    implementation "com.h2database:h2:2.0.206"
    implementation "mysql:mysql-connector-java:8.0.27"
    implementation "org.postgresql:postgresql:42.3.1"
    implementation "com.microsoft.sqlserver:mssql-jdbc:9.4.1.jre11"
    implementation "com.zaxxer:HikariCP:5.0.1"
    implementation "io.netty:netty-all:4.1.66.Final"
    implementation "org.slf4j:slf4j-jdk14:2.0.0-alpha6"
    implementation "com.google.inject:guice:$guiceVersion"
    implementation "com.google.inject.extensions:guice-assistedinject:$guiceVersion"
    implementation "org.owasp.encoder:encoder:1.2.3"
    implementation "org.glassfish:javax.json:1.1.4"
    implementation "org.eclipse.jetty:jetty-server:$jettyVersion"
    implementation "org.eclipse.jetty:jetty-servlet:$jettyVersion"
    implementation "org.eclipse.jetty:jetty-webapp:$jettyVersion"
    implementation "org.eclipse.jetty:jetty-jndi:$jettyVersion"
    implementation "org.eclipse.jetty:jetty-proxy:$jettyVersion"
    implementation "org.eclipse.jetty.websocket:websocket-jetty-server:$jettyVersion"
    implementation "org.glassfish.jersey.containers:jersey-container-servlet:$jerseyVersion"
    implementation "org.glassfish.jersey.media:jersey-media-json-jackson:$jerseyVersion"
    implementation "org.glassfish.jersey.inject:jersey-hk2:$jerseyVersion"
    implementation "org.glassfish.hk2:guice-bridge:2.6.1" // same version as jersey-hk2
    implementation "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:$jacksonVersion"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr353:$jacksonVersion"
    implementation "org.liquibase:liquibase-core:4.7.0"
    implementation "com.sun.mail:javax.mail:1.6.2"
    implementation "org.jxls:jxls:2.4.7" // needs upgrade (wait for jexl 4)
    implementation "org.jxls:jxls-poi:1.0.16" // needs upgrade (wait for jexl 4)
    implementation "org.apache.velocity:velocity:1.7"
    implementation "org.apache.velocity:velocity-tools:2.0"
    implementation "org.apache.commons:commons-collections4:4.4"
    implementation "org.mnode.ical4j:ical4j:3.1.2"
    implementation "org.locationtech.spatial4j:spatial4j:0.8"
    implementation "org.locationtech.jts:jts-core:1.18.2"
    implementation "net.java.dev.jna:jna-platform:5.10.0"
    implementation "com.github.jnr:jnr-posix:3.1.15"
    implementation "com.google.protobuf:protobuf-java:$protobufVersion"
    implementation "javax.xml.bind:jaxb-api:2.3.1"
    implementation "com.sun.xml.bind:jaxb-core:3.0.2"
    implementation "com.sun.xml.bind:jaxb-impl:3.0.2"
    implementation "javax.activation:activation:1.1.1"
    implementation "com.amazonaws:aws-java-sdk-sns:1.12.141"
    testImplementation "junit:junit:4.13.2"
}

task copyDependencies(type: Copy) {
    into "$projectDir/target/lib"
    from configurations.runtimeClasspath
}
assemble.dependsOn(copyDependencies)

jar {
    manifest {
        attributes(
                "Main-Class": "org.traccar.Main",
                "Implementation-Version": "4.15",
                "Class-Path": configurations.runtimeClasspath.files.collect { "lib/$it.name" }.join(" "))
    }
}
